#
#    Copyright 2017-2021 Kai Pastor
#    
#    This file is part of OpenOrienteering.
# 
#    OpenOrienteering is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
# 
#    OpenOrienteering is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
# 
#    You should have received a copy of the GNU General Public License
#    along with OpenOrienteering.  If not, see <http://www.gnu.org/licenses/>.

# The generated custom install script `install-html.cmake` is run at install
# time. Here, `file(INSTALL ...)` must be called with the absolute path of
# the destination, and so we must explicitly take care of adding
# `CMAKE_INSTALL_PREFIX` when needed.
# This is different from using `file(INSTALL ...)` at configuration time
# where CMake takes care of `CMAKE_INSTALL_PREFIX` for generated scripts.

get_filename_component(destination "@MAPPER_ABOUT_DESTINATION@/manual" ABSOLUTE
  BASE_DIR "${CMAKE_INSTALL_PREFIX}"
)

file(READ "@CMAKE_CURRENT_BINARY_DIR@/html/index.qhp" index_qhp)
string(REGEX MATCHALL "<file>[^<]*</file>" matches "${index_qhp}")
foreach(match ${matches})
	string(REGEX REPLACE ".*<file>" "" match "${match}")
	string(REGEX REPLACE "</file>.*" "" match "${match}")
	file(INSTALL
	  DESTINATION "${destination}"
	  FILES "@CMAKE_CURRENT_BINARY_DIR@/html/${match}"
	)
endforeach()
